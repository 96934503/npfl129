#set term svg size 800,600 enhanced font 'Times, 18'
#set output 'partial_func_eg.svg'
set term pdf size 8,6 enhanced font 'Times, 18'
set output 'partial_func_eg.pdf'

set multiplot

set xlabel "x"
set ylabel "y"
set zlabel "z"
set xrange [-2.5:2.5]
set yrange [-2.5:2.5]
set zrange [-1.0:10.0]
unset colorbox
set view 50,20
set style fill solid 0.6

set border lw 0.5 
unset key
set xlabel font 'Times, 48'
set ylabel font 'Times, 48'
set zlabel font 'Times, 48'

set hidden3d front
set ticslevel 0
set isosamples 20,200

# Draw the surfaces of f(x,y) = x^2 + xy + y^2 and y=1.
set palette defined (0 "navy", 10 "cyan")
splot	x*x + x*y + y*y w pm3d, \
		y<0.99 ? -2 : (y>1.04 ? 10.01 : (y<1 ? -1 : 10)) w lines lt 8 lw 2

# Draw the grids and intersection curve.
set isosamples 40,40
splot	x*x + x*y + y*y w lines lt 8 lw 0.1
splot	0.95<y && y<1.05 ? x*x + x + 1 : -2 w lines lt 7 lw 2
